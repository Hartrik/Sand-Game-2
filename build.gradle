
allprojects {
    apply plugin: 'java'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile group: 'com.github.Hartrik', name: 'HS-Common-Library', version: '1.0.0'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

subprojects {
    sourceSets {
        main {
            java {
                srcDir 'src'
            }
            resources {
                srcDirs 'src', 'src-resources'
                excludes ['**/*.java']
            }
        }
        test {
            java {
                srcDir 'test'
            }
            resources {
                srcDirs 'test', 'test-resources'
                excludes ['**/*.java']
            }
        }
    }
}

dependencies {
    compile project(':sg2-app')
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Sand Game 2',
                'Implementation-Version': '2.04 Beta',
                'Implementation-Vendor': 'Patrik Harag',
                'Main-Class': 'cz.hartrik.sg2.app.sandbox.Main',
        )
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

test {
    // console output
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}
